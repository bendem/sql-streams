plugins {
    id 'java'
    id 'maven'
    id 'jacoco'
}

group 'be.bendem'
version '0.1.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.xerial:sqlite-jdbc:3.8.11.2'
    testCompile 'org.postgresql:postgresql:9.4.1208'
}

install {
    repositories.mavenInstaller {
        pom.artifactId = 'sql-streams'
    }
}

javadoc {
    exclude '**/impl/*.java'
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links 'https://docs.oracle.com/javase/8/docs/api/'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

test {
    useJUnit()

    testLogging.exceptionFormat = 'full'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
